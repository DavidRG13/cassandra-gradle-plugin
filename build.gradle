plugins {
    id 'groovy'
    id 'maven'
    id 'java-gradle-plugin'
    id "com.gradle.plugin-publish" version "0.9.7"
}
apply plugin: 'signing'

group GROUP
version VERSION_NAME

repositories {
    mavenLocal()
    jcenter()
}

dependencies {
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4', {
        exclude module: 'groovy-all'
    }
}

pluginBundle {
  website = POM_URL
  vcsUrl = POM_URL
  description = POM_DESCRIPTION
  tags = ['cassandra', 'cassandraDb']

  plugins {
    cassandraPlugin {
      id = 'com.github.william-hill-online.cassandra'
      displayName = 'Gradle Cassandra plugin'
    }
  }
}

task createPluginClasspath {
    def outputDir = file("${buildDir}/resources/test")

    inputs.files sourceSets.test.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("${outputDir}/plugin-classpath.txt").text = sourceSets.test.runtimeClasspath.join('\n',)
    }
}

test.dependsOn createPluginClasspath

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}

def isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // POM signature
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            // Target repository
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId = GROUP
            pom.artifactId = POM_ARTIFACT_ID
            pom.version = VERSION_NAME

            pom.project {
                name POM_NAME
                description POM_DESCRIPTION
                packaging 'jar'
                url POM_URL

                scm {
                    connection POM_SCM_CONNECTION
                    developerConnection POM_SCM_DEV_CONNECTION
                    url POM_SCM_URL
                }

                licenses {
                    license {
                        name POM_LICENCE_NAME
                        url POM_LICENCE_URL
                        distribution POM_LICENCE_DIST
                    }
                }

                developers {
                    developer {
                        id = 'davidrg13'
                        name = 'davidrg13'
                    }
                }
            }
        }
    }
}